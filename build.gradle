// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.jakewharton:butterknife-gradle-plugin:10.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.5.2.0"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.10.0"
}

configurations {
    ktlint
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/spekframework/spek-dev" }
    }
}

repositories {
    jcenter()
}

tasks.detekt.jvmTarget = "1.8"

detekt {
    input = files("$projectDir")
    config = files("$project.projectDir/config/detekt/detekt.yml")
    reports {
        xml {
            enabled = true
            destination = file("$project.projectDir/detekt-report.xml")
        }
        html {
            enabled = true
            destination = file("$project.projectDir/detekt-report.html")
        }
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.10.0"

    ktlint "com.pinterest:ktlint:0.37.2"
}