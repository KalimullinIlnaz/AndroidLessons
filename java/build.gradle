apply plugin: 'kotlin'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

checkstyle {
    toolVersion = '8.1'
}

pmd {
    toolVersion = '6.8.0'
    ruleSets = []
}

repositories {
    jcenter()
}

configurations {
    ktlint
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
}

def ktLintClasspath = configurations.ktlint
def ktLintMain = "com.pinterest.ktlint.Main"
def ktLintArgs = "src/**/*.kt"

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = ktLintClasspath
    main = ktLintMain
    args ktLintArgs
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = ktLintClasspath
    main = ktLintMain
    args "-F", ktLintArgs
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    ktlint "com.pinterest:ktlint:0.37.2"

    // assertion
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    def spek_version = '2.0.12'
    //spek2
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    //RxJava
    implementation "io.reactivex.rxjava3:rxjava:3.0.4"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"
    
    //JUnit
    testImplementation('junit:junit:4.13')

    //Mockito
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation('org.mockito:mockito-core:3.4.4')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task checkstyle(type: Checkstyle) {
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'

    // empty classpath
    classpath = files()
}

task pmd(type: Pmd) {
    description 'Identifying potential problems mainly dead code, duplicated code, cyclomatic complexity and overcomplicated expressions'
    group 'verification'
    ruleSetFiles = files("${rootDir}/config/pmd/rules-pmd.xml")
    source = fileTree('src/main/java')
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        html.destination file("$project.buildDir/outputs/pmd/pmd.html")
    }
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}