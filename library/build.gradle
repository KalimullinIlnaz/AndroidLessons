apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'checkstyle'
apply plugin: 'pmd'

apply plugin: 'com.jakewharton.butterknife'

apply plugin: 'de.mannodermaus.android-junit5'

checkstyle {
    toolVersion = '8.1'
}

pmd {
    toolVersion = '6.8.0'
    ruleSets = []
}

repositories {
    jcenter()
}

configurations {
    ktlint
}


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        lintConfig file("$project.rootDir/config/lint/lint.xml")

        abortOnError false
        checkAllWarnings true
        warningsAsErrors true
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task checkstyle(type: Checkstyle) {
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

task pmd(type: Pmd) {
    description 'Identifying potential problems mainly dead code, duplicated code, cyclomatic complexity and overcomplicated expressions'
    group 'verification'
    ruleSetFiles = files("${rootDir}/config/pmd/rules-pmd.xml")
    source = fileTree('src/main/java')
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        html.destination file("$project.buildDir/outputs/pmd/pmd.html")
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':java')

    ktlint "com.pinterest:ktlint:0.37.2"

    //coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8'

    // assertion
    testImplementation "org.jetbrains.kotlin:kotlin-test:1.3.72"

    def spek_version = '2.0.12'
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"

    //Mockito
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.mockito:mockito-core:3.4.4"
    androidTestImplementation 'org.mockito:mockito-android:3.4.4'

    //JUnit
    testImplementation('junit:junit:4.13')
    testImplementation 'androidx.test.ext:junit:1.1.1'

    //Butter Knife
    implementation 'com.jakewharton:butterknife:10.2.1'
    kapt 'com.jakewharton:butterknife-compiler:10.2.1'

    //Decode points
    implementation 'com.google.maps.android:android-maps-utils:2.0.3'

    //Moxy
    implementation "com.github.moxy-community:moxy:2.1.2"
    implementation "com.github.moxy-community:moxy-app-compat:2.1.2"
    kapt "com.github.moxy-community:moxy-compiler:2.1.2"

    //Recyclerview
    implementation 'com.android.support:recyclerview-v7:29.0.0'

    //GSON
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    //Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.0'

    //Google Maps
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    //RxJava
    implementation "io.reactivex.rxjava3:rxjava:3.0.4"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    //Rx-Retrofit call adapter
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0"

    //CircularProgressView
    implementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'

    //Dagger2
    implementation 'com.google.dagger:dagger:2.28.3'
    kapt'com.google.dagger:dagger-compiler:2.28.3'
    implementation 'com.google.dagger:dagger-android:2.28.3'
    implementation 'com.google.dagger:dagger-android-support:2.28.3'
    kapt'com.google.dagger:dagger-android-processor:2.28.3'

    //Room
    implementation "androidx.room:room-runtime:2.2.5"
    kapt"androidx.room:room-compiler:2.2.5"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.support:appcompat-v7:29.0.0'
    implementation 'com.google.android.material:material:1.1.0'
}
